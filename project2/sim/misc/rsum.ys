#Rulin Huang 5140379001
# Execution begins at address 0
			.pos 	0		
init:	
			irmovl 	Stack, %esp	    # Set up stack pointer
			irmovl 	Stack, %ebp	    # Set up base pointer
			call 	Main		    # Execute main program
			halt				    # Terminate program

# Sample linked list
.align 4
ele1:
			.long 0x00a
			.long ele2
ele2:
			.long 0x0b0
			.long ele3
ele3:
			.long 0xc00
			.long 0

Main:		
			pushl	%ebp			# Push %ebp into the stack
			rrmovl	%esp, %ebp
			irmovl	ele1, %eax		# Move pointer to ele1 into %eax
			pushl	%eax			# Push %eax into the stack 

			call 	rsum_list
			rrmovl	%ebp, %esp
			popl	%ebp
			ret

#long rsum_list(list_ptr ls)

rsum_list:	
			pushl	%ebp
			rrmovl	%esp, %ebp
			mrmovl	8(%ebp), %ecx	# %ecx = ele1
			andl	%ecx, %ecx		# set condition codes
			jne		Loop
			xorl	%eax, %eax
			jmp		End
			
Loop:		
			mrmovl	(%ecx), %eax	# get *ls
			pushl	%eax
			mrmovl 	4(%ecx), %ecx 	# ls->next
			pushl	%ecx
			call	rsum_list
			mrmovl	-4(%ebp), %ebx
			addl	%ebx, %eax		# add to sum
			
End:		
			rrmovl	%ebp, %esp
			popl	%ebp
			ret

# The stack starts here and grows to lower addresser
			.pos	0x100
Stack:
